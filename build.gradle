// Top-level build file where you can 
// add configuration options common 
// to all sub-projects/modules.

buildscript
{
    repositories
    {
        mavenLocal()
        mavenCentral()
    }

    dependencies
    {
        classpath 'com.android.tools.build:gradle:0.5.+'
        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.+'
    }

}

// default settings
ext.compileSdkVersion = 18
ext.buildToolsVersion = "18.0.1"
ext.minSdkVersion = 8
ext.targetSdkVersion = 18

// Project properties
project.group = 'com.qozix'
project.version = '1.0.0'

apply plugin: 'android-library'
apply plugin: 'signing'

repositories
{
    mavenLocal()
    mavenCentral()
}

dependencies
{
    compile 'com.android.support:support-v4:13.0.+'
    compile 'com.jakewharton:disklrucache:1.3.1'
}

android
{
    buildToolsVersion "$androidBuildToolsVersion"
    compileSdkVersion androidCompileSdkVersion
    
    defaultConfig
    {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
    }

    sourceSets
    {
        main
        {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

apply plugin: 'maven'
configurations
{
    archives
    { extendsFrom configurations.default }
}

signing
{
    required
    {
        has("release") && gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}

def localRepoPath = "file://" +  new File(System.getProperty("user.home"), ".m2/repository").absolutePath

uploadArchives 
{
    configuration = configurations.archives
    repositories.mavenDeployer 
    {
        beforeDeployment 
        { 
            MavenDeployment deployment -> signing.signPom(deployment) 
        }

        repository(url: localRepoPath) 
        
        pom.artifactId = 'tileview'
        pom.project 
        {
            packaging 'aar'

            licenses 
            {
                license 
                {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}